main
  /*
The number, 1406357289, is a 0 to 9 pandigital number because it is made up of each of the digits 0 to 9 in some order, but it also has a rather interesting sub-string divisibility property.

Let d1 be the 1st digit, d2 be the 2nd digit, and so on. In this way, we note the following:

d2d3d4=406 is divisible by 2
d3d4d5=063 is divisible by 3
d4d5d6=635 is divisible by 5
d5d6d7=357 is divisible by 7
d6d7d8=572 is divisible by 11
d7d8d9=728 is divisible by 13
d8d9d10=289 is divisible by 17
Find the sum of all 0 to 9 pandigital numbers with this property.

d4 % 2 == 0
(d3 + d4 + d5) % 3 == 0
d6 = 5 ou d6 = 0
(d5 * 100 + d6 * 10 + d7  ) % 7 == 0
(d6 * 100 + d7 * 10 + d8  ) % 11 == 0
(d7 * 100 + d8 * 10 + d9  ) % 13 == 0
(d8 * 100 + d9 * 10 + d10 ) % 17 == 0


d4 % 2 == 0
d6 = 5 ou d6 = 0

(d3 + d4 + d5) % 3 == 0
(d5 * 2 + d6 * 3 + d7) % 7 == 0
*/
  def array<bool> allowed[10] with i do
                                       return true
  end
  for i6 = 0 to  1
  do
    def int d6 = i6 * 5
    if allowed[d6]
    then
      allowed[d6] = false
      for d7 = 0 to  9
      do
        if allowed[d7]
        then
          allowed[d7] = false
          for d8 = 0 to  9
          do
            if allowed[d8]
            then
              allowed[d8] = false
              for d9 = 0 to  9
              do
                if allowed[d9]
                then
                  allowed[d9] = false
                  for d10 = 1 to  9
                  do
                    if allowed[d10] && (d6 * 100 + d7 * 10 + d8) % 11 == 0 && (d7 * 100 + d8 * 10 + d9) % 13 == 0 && (d8 * 100 + d9 * 10 + d10) % 17 == 0
                    then
                      allowed[d10] = false
                      for d5 = 0 to  9
                      do
                        if allowed[d5]
                        then
                          allowed[d5] = false
                          if (d5 * 100 + d6 * 10 + d7) % 7 == 0
                          then
                            for i4 = 0 to  4
                            do
                              def int d4 = i4 * 2
                              if allowed[d4]
                              then
                                allowed[d4] = false
                                for d3 = 0 to  9
                                do
                                  if allowed[d3]
                                  then
                                    allowed[d3] = false
                                    if (d3 + d4 + d5) % 3 == 0
                                    then
                                      for d2 = 0 to  9
                                      do
                                        if allowed[d2]
                                        then
                                          allowed[d2] = false
                                          for d1 = 0 to  9
                                          do
                                            if allowed[d1]
                                            then
                                              allowed[d1] = false
                                              print int d1
                                              print int d2
                                              print int d3
                                              print int d4
                                              print int d5
                                              print int d6
                                              print int d7
                                              print int d8
                                              print int d9
                                              print int d10
                                              print string " + "
                                              allowed[d1] = true
                                            end
                                          end
                                          allowed[d2] = true
                                        end
                                      end
                                    end
                                    allowed[d3] = true
                                  end
                                end
                                allowed[d4] = true
                              end
                            end
                          end
                          allowed[d5] = true
                        end
                      end
                      allowed[d10] = true
                    end
                  end
                  allowed[d9] = true
                end
              end
              allowed[d8] = true
            end
          end
          allowed[d7] = true
        end
      end
      allowed[d6] = true
    end
  end
  print int 0
  print string "\n"
end

