/* Ce code a été généré par metalang
   Il gère les entrées sorties pour un programme dynamique classique
   dans les épreuves de prologin
on le retrouve ici : http://projecteuler.net/problem=18
*/

def int find0(int len, array<array<int>> tab, array<array<int>> cache, int x, int y)
	/*
	Cette fonction est récursive
	*/
	if y == len - 1 then
	  return tab[y][x]
	elsif x > y then
	  return -10000
	elsif cache[y][x] != 0 then
	  return cache[y][x]
	end
	def int result = 0
	def int out0 = find0(len, tab, cache, x, y+1)
	def int out1 = find0(len, tab, cache, x+1, y+1)
	if out0 > out1 then
	  result = out0 + tab[y][x]
	else
	  result = out1 + tab[y][x]
	end
	cache[y][x] = result
	return result
end

def int find(int len, array<array<int>> tab)
	def array<array<int>> tab2[len] with i do
	  def array<int> tab3[i+1] with j do
			return 0
		end
	  return tab3
	end
	return find0(len, tab, tab2, 0, 0)
end

main
	def read int len skip
	def array<array<int>> tab[len] with i do
		def array<int> tab2[i+1] with j do
			def read int tmp skip
			return tmp
		end
		return tab2
	end
	print int find(len, tab) print "\n"
  for k = 0 to len - 1 do
    for l = 0 to k do
	    print int tab[k][l] print " "
    end
    print string "\n"
  end
end
