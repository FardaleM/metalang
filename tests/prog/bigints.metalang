record @bigint
	bigint_sign : bool
	bigint_len : int
  bigint_chiffres : array<int>
end

def @bigint read_bigint(int len)

	def array<int> chiffres[len] with j do
		def read char c
		return int_of_char(c)
	end

	for i = 0 to (len - 1) / 2 do
		def tmp = chiffres[i]
		chiffres[i] = chiffres[len - 1 - i]
		chiffres[len - 1 - i] = tmp
	end

	return record
		bigint_sign = true
		bigint_len = len
		bigint_chiffres = chiffres
	end
end

def void print_bigint(@bigint a)
	if !a.bigint_sign then
		print '-'
	end
	for i = 0 to a.bigint_len - 1 do
		print a.bigint_chiffres[a.bigint_len - 1 - i]
	end
end

def bool bigint_eq(@bigint a, @bigint b)
/* Renvoie vrai si a = b */
	if a.bigint_sign != b.bigint_sign then
		return false
	elsif a.bigint_len != b.bigint_len then
		return false
	else
		for i = 0 to a.bigint_len - 1 do
			if a.bigint_chiffres[i] != b.bigint_chiffres[i] then
				return false
			end
		end
		return true
	end
end

def bool bigint_gt(@bigint a, @bigint b)
/* Renvoie vrai si a > b */
	if a.bigint_sign && !b.bigint_sign then
		return true
	elsif !a.bigint_sign && b.bigint_sign then
		return false
	else
		if a.bigint_len > b.bigint_len then
			return a.bigint_sign
		elsif a.bigint_len < b.bigint_len then
			return !a.bigint_sign
		else
			for i = 0 to a.bigint_len - 1 do
				def j = a.bigint_len - 1 - i
				if a.bigint_chiffres[j] > b.bigint_chiffres[j] then
					return a.bigint_sign
				elsif a.bigint_chiffres[j] < b.bigint_chiffres[j] then
					return ! a.bigint_sign
				end
			end
		end
		return true
	end
end

def bool bigint_lt(@bigint a, @bigint b)
	return !bigint_gt(a, b)
end

def @bigint add_bigint_positif(@bigint a, @bigint b)
/* Une addition ou on en a rien a faire des signes */
	def len = max2(a.bigint_len, b.bigint_len) + 1
	def retenue = 0
	def array<int> chiffres[len] with i do
		def tmp = retenue
		if i < a.bigint_len then
			tmp += a.bigint_chiffres[i]
		end
		if i < b.bigint_len then
			tmp += b.bigint_chiffres[i]
		end
		retenue = tmp / 10
		return tmp % 10
  end
	while len > 0 && chiffres[len - 1] == 0 do
		len --
	end
	return record
		bigint_sign = true
		bigint_len = len
		bigint_chiffres = chiffres
	end
end

def @bigint sub_bigint_positif(@bigint a, @bigint b)
/* Une soustraction ou on en a rien a faire des signes
Pré-requis : a > b
*/
	def len = a.bigint_len
	def retenue = 0
	def array<int> chiffres[len] with i do
		def tmp = retenue + a.bigint_chiffres[i]
		if i < b.bigint_len then
			tmp -= b.bigint_chiffres[i]
		end
		if tmp < 0 then
			tmp += 10
			retenue = -1
		else
			retenue = 0
		end
		return tmp
  end
	while len > 0 && chiffres[len - 1] == 0 do
		len --
	end
	return record
		bigint_sign = true
		bigint_len = len
		bigint_chiffres = chiffres
	end
end

def @bigint neg_bigint(@bigint a)
	return record
		bigint_sign = !a.bigint_sign
		bigint_len = a.bigint_len
		bigint_chiffres = a.bigint_chiffres
	end
end

def @bigint add_bigint(@bigint a, @bigint b)
	if a.bigint_sign == b.bigint_sign then
		if a.bigint_sign then
			return add_bigint_positif(a, b)
		else
			return neg_bigint(add_bigint_positif(a, b))
		end
	elsif a.bigint_sign then /* a positif, b negatif */
		if bigint_gt(a, neg_bigint(b)) then
			return sub_bigint_positif(a, b)
		else
			return neg_bigint(sub_bigint_positif(b, a))
		end
	else /* a negatif, b positif */
		if bigint_gt(neg_bigint(a), b) then
			return neg_bigint(sub_bigint_positif(a, b))
		else
			return sub_bigint_positif(b, a)
		end
  end
end

def @bigint sub_bigint(@bigint a, @bigint b)
	return add_bigint(a, neg_bigint(b))
end

def @bigint mul_bigint_cp(@bigint a, @bigint b)
/* Cet algorithm est quadratique.
C'est le même que celui qu'on enseigne aux enfants en CP.
D'ou le nom de la fonction. */
	def len = a.bigint_len + b.bigint_len + 1
	def array<int> chiffres[len] with k do
		return 0
	end
	for i = 0 to a.bigint_len - 1 do
		def retenue = 0
		for j = 0 to b.bigint_len - 1 do
			chiffres[i + j] += retenue + b.bigint_chiffres[j] * a.bigint_chiffres[i];
			retenue = chiffres[i + j] / 10
			chiffres[i + j] = chiffres[i + j] % 10
		end
		chiffres[i + b.bigint_len] += retenue
	end
	chiffres[a.bigint_len + b.bigint_len] = chiffres[a.bigint_len + b.bigint_len - 1] / 10
	chiffres[a.bigint_len + b.bigint_len - 1] = chiffres[a.bigint_len + b.bigint_len - 1] % 10
	for l = 0 to 2 do
		if len != 0 && chiffres[len - 1] == 0 then
			len --
		end
	end
	return record
		bigint_sign = a.bigint_sign == b.bigint_sign
		bigint_len = len
		bigint_chiffres = chiffres
	end
end

def @bigint bigint_premiers_chiffres(@bigint a, int i)
	def len = min2(i, a.bigint_len)
	while len != 0 && a.bigint_chiffres[len - 1] == 0 do
		len --
	end
	return record
		bigint_sign = a.bigint_sign
		bigint_len = len
		bigint_chiffres = a.bigint_chiffres
	end
end

def @bigint bigint_shift(@bigint a, int i)
	def array<int> chiffres[a.bigint_len + i] with k do
		if k >= i then
			return a.bigint_chiffres[k - i]
		else
			return 0
		end
	end
	return record
		bigint_sign = a.bigint_sign
		bigint_len = a.bigint_len + i
		bigint_chiffres = chiffres
	end
end


def @bigint mul_bigint(@bigint aa, @bigint bb)
	if aa.bigint_len == 0 then
		return aa
	elsif bb.bigint_len == 0 then
		return bb
	elsif aa.bigint_len < 3 || bb.bigint_len < 3 then
		return mul_bigint_cp(aa, bb)
	end
	/* Algorithme de Karatsuba */
	def split = min2(aa.bigint_len, bb.bigint_len) / 2
	def a = bigint_shift(aa, -split)
	def b = bigint_premiers_chiffres(aa, split)
	def c = bigint_shift(bb, -split)
	def d = bigint_premiers_chiffres(bb, split)
	def amoinsb = sub_bigint(a, b);
	def cmoinsd = sub_bigint(c, d);
	def ac = mul_bigint(a, c)
	def bd = mul_bigint(b, d)
	def amoinsbcmoinsd = mul_bigint(amoinsb, cmoinsd)
	def acdec = bigint_shift(ac, 2 * split)
	return add_bigint(add_bigint(acdec, bd),
	bigint_shift(sub_bigint(add_bigint(ac, bd), amoinsbcmoinsd), split))
/* ac × 102k + (ac + bd – (a – b)(c – d)) × 10k + bd */
end

/*
Division,
Modulo
*/


def int log10(int a)
	def out = 1
	while a >= 10 do
		a /= 10
		out ++
	end
	return out
end

def @bigint bigint_of_int(int i)
	def size = log10(i)
	if i == 0 then
		size = 0
	end
	def array<int> t[size] with j do
		return 0
	end
	for k = 0 to size - 1 do
		t[k] = i % 10
		i /= 10
	end
	return record
		bigint_sign = true
		bigint_len = size
		bigint_chiffres = t
	end
end

def @bigint fact_bigint(@bigint a)
	def one = bigint_of_int(1)
	def out = one
	while ! bigint_eq(a, one) do
		out = mul_bigint(a, out)
		a = sub_bigint(a, one)
	end
	return out
end

def int sum_chiffres_bigint(@bigint a)
	def out = 0
	for i = 0 to a.bigint_len - 1 do
		out += a.bigint_chiffres[i]
	end
	return out
end

/* http://projecteuler.net/problem=20 */
def int euler20()
	def a = bigint_of_int(100)
	a = fact_bigint(a)
	return sum_chiffres_bigint(a)
end

def @bigint bigint_exp(@bigint a, int b)
	if b == 1 then
		return a
	elsif b % 2 == 0 then
		return bigint_exp(mul_bigint(a, a), b / 2)
	else
		return mul_bigint(a, bigint_exp(a, b - 1))
	end
end

def @bigint bigint_exp_10chiffres(@bigint a, int b)
	a = bigint_premiers_chiffres(a, 10)
	if b == 1 then
		return a
	elsif b % 2 == 0 then
		return bigint_exp_10chiffres(mul_bigint(a, a), b / 2)
	else
		return mul_bigint(a, bigint_exp_10chiffres(a, b - 1))
	end
end

def void euler48()
	def sum = bigint_of_int(0)
	for i = 1 to 1000 do
		def ib = bigint_of_int(i)
		def ibeib = bigint_exp_10chiffres(ib, i)
		sum = add_bigint(sum, ibeib)
		sum = bigint_premiers_chiffres(sum, 10)
  end
	print "euler 48 = " print_bigint(sum) print "\n"
end

def int euler16()
	def a = bigint_of_int(2)
	a = bigint_exp(a, 1000)
	return sum_chiffres_bigint(a)
end

def int euler25()
	def i = 2
  def a = bigint_of_int(1)
  def b = bigint_of_int(1)
  while b.bigint_len < 1000 do
		def c = add_bigint(a, b)
		a = b
		b = c
		i ++
  end
	return i
end

def int euler29()
	def maxA = 5
	def maxB = 5
	def array<@bigint> a_bigint[maxA + 1] with j do
		return bigint_of_int(j * j)
	end
	def array<@bigint> a0_bigint[maxA + 1] with j2 do
		return bigint_of_int(j2)
	end
	def array<int> b[maxA + 1] with k do
		return 2
	end
	def n = 0
	def found = true
	while found do
		def min = a0_bigint[0]
		found = false
		for i = 2 to maxA do
			if b[i] <= maxB then
				if found then
					if bigint_lt(a_bigint[i], min) then
						min = a_bigint[i]
					end
				else
					min = a_bigint[i]
					found = true
				end
			end
		end
		if found then
			n++
			for l = 2 to maxA do
				if bigint_eq(a_bigint[l], min) && b[l] <= maxB then
					b[l] ++
					a_bigint[l] = mul_bigint(a_bigint[l], a0_bigint[l])
				end
			end
		end
	end
	return n
end

main
	print euler29() print "\n"
	def sum = read_bigint(50)
	for i = 2 to 100 do
		skip
		def tmp = read_bigint(50)
		sum = add_bigint(sum, tmp)
	end

	print "euler13 = " print_bigint(sum) print "\n"
	print "euler25 = " print (euler25()) print "\n"
	print "euler16 = " print euler16() print "\n"
	euler48()
	print "euler20 = " print euler20() print "\n"

	def a = bigint_of_int(999999)
	def b = bigint_of_int(9951263)
	print_bigint(a) print ">>1="
	print_bigint(bigint_shift(a, -1))
	print "\n"
	print_bigint(a) print "*"
	print_bigint(b)  print "="
  print_bigint( mul_bigint(a, b) )
	print "\n"
	print_bigint(a) print "*"
	print_bigint(b)  print "="
  print_bigint( mul_bigint_cp(a, b) )
	print "\n"
	print_bigint(a) print "+"
	print_bigint(b)  print "="
  print_bigint( add_bigint(a, b) )
	print "\n"
	print_bigint(b) print "-"
	print_bigint(a)  print "="
  print_bigint( sub_bigint(b, a) )
	print "\n"
	print_bigint(a) print "-"
	print_bigint(b)  print "="
  print_bigint( sub_bigint(a, b) )
	print "\n"
	print_bigint(a) print ">"
	print_bigint(b)  print "="
  print bigint_gt(a, b)
	print "\n"
end
